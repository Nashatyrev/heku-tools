plugins {
    id 'org.jetbrains.kotlin.jvm' version '1.6.21'
    id 'java'
    id 'io.spring.dependency-management' version '1.1.0'
    id 'maven-publish'
}

subprojects {
    group 'tech.pegasys.heku'
    version '1.0-SNAPSHOT'

    apply plugin: "java"
    apply plugin: "kotlin"
    apply plugin: "application"
    apply plugin: 'io.spring.dependency-management'
    apply plugin: "maven-publish"
    apply from: "${rootDir}/versions.gradle"

    repositories {
        mavenLocal()
        mavenCentral()
        maven { url "https://artifacts.consensys.net/public/maven/maven/" }
        maven { url "https://dl.cloudsmith.io/public/libp2p/jvm-libp2p/maven/" }
        maven { url "https://hyperledger.jfrog.io/artifactory/besu-maven/" }
        maven { url "https://artifacts.consensys.net/public/teku/maven/" }
        maven { url "https://mvnrepository.com/" }
    }

    compileKotlin {
        kotlinOptions.languageVersion = "1.6"
        kotlinOptions.jvmTarget = "11"
        kotlinOptions.freeCompilerArgs = ["-Xjvm-default=enable", "-Xopt-in=kotlin.RequiresOptIn"]
    }
    compileTestKotlin {
        kotlinOptions.languageVersion = "1.6"
        kotlinOptions.jvmTarget = "11"
    }

    dependencies {
        implementation "org.jetbrains.kotlin:kotlin-stdlib"
        implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-reactive'
        implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-jdk8'

        testImplementation 'org.junit.jupiter:junit-jupiter-api'
        testImplementation 'org.assertj:assertj-core'

        testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine'
    }

    test {
        useJUnitPlatform()
    }

    publishing {
        if (project.hasProperty("mavenArtifactId")) {
            println(project.name + ": " + project.ext.mavenArtifactId)
            publications {
                maven(MavenPublication) {
                    groupId project.group
                    artifactId project.ext.mavenArtifactId
                    version project.version
                    from components.java
                }
            }
        }
    }
}
